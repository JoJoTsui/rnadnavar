//
// MERGE INDEX CRAM
//
// For all modules here:
// A when clause condition is defined in the conf/modules.config to determine if the module should be run

include { SAMTOOLS_INDEX as INDEX_CRAM } from '../../../modules/nf-core/samtools/index/main'
include { SAMTOOLS_MERGE as MERGE_CRAM } from '../../../modules/nf-core/samtools/merge/main'

workflow CRAM_MERGE_INDEX_SAMTOOLS {
    take:
    cram      // channel: [mandatory] meta, cram (list of crams)
    fasta     // channel: [mandatory] [[id:'fasta'], fasta] - pre-formatted (value channel)
    fasta_fai // channel: [mandatory] [[id:'fai'], path] - pre-formatted (value channel)

    main:
    versions = Channel.empty()

    // Figuring out if there is one or more cram(s) from the same sample
    cram_to_merge = cram.branch{ meta, c ->
        // c is a list, so use c.size() to asses number of intervals
        single:   c.size() <= 1
            return [ meta, c[0] ]
        multiple: c.size() > 1
    }

    // Only when using intervals
    // fasta and fasta_fai are pre-formatted value channels that broadcast to all MERGE_CRAM invocations
    // gzi is empty but needs to be a tuple for SAMTOOLS_MERGE
    MERGE_CRAM(cram_to_merge.multiple, fasta, fasta_fai, [[id:'gzi'], []])

    // Mix intervals and no_intervals channels together
    cram_all = MERGE_CRAM.out.cram.mix(cram_to_merge.single)

    // Index cram
    INDEX_CRAM(cram_all)

    // Join with the crai file
    cram_crai = cram_all.join(INDEX_CRAM.out.crai, failOnDuplicate: true, failOnMismatch: true)

    // Gather versions of all tools used
    versions = versions.mix(INDEX_CRAM.out.versions.first())
    versions = versions.mix(MERGE_CRAM.out.versions.first())

    emit:
    cram_crai

    versions
}
